import io.gitlab.arturbosch.detekt.Detekt

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

plugins {
    id 'com.android.application' version '8.11.2' apply false
    id 'com.android.library' version '8.11.2' apply false
    id 'org.jetbrains.kotlin.android' version '2.1.20' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.8'
}

tasks.withType(Detekt).configureEach {
    // Files to analyze
    setSource(files(rootProject.rootDir))

    // Configuration
    config.setFrom(files("$projectDir/detekt.yml"))
    allRules = true
    buildUponDefaultConfig = true

    reports {
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.file("reports/detekt/detekt-report.html"))

        xml.required.set(true)
        xml.outputLocation.set(layout.buildDirectory.file("reports/detekt/detekt-checkstyle.xml"))
    }
}


tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

allprojects {
    group = 'com.telefonica.nestedscrollwebview'
    if (System.getProperty("SNAPSHOT_VERSION") != null) {
        version = System.getProperty("SNAPSHOT_VERSION")+"-SNAPSHOT"
    } else {
        version = System.getProperty("LIBRARY_VERSION") ?: "undefined"
    }
}
apply from: "${rootProject.projectDir}/publish_maven_central.gradle"
